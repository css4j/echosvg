plugins {
	id 'echosvg.java-conventions'
}

sourceSets {
	// Tests
	test {
		resources.srcDirs += ["$rootDir/test-resources"]
	}
	// Interactive tests
	itest {
		java.srcDirs = ['src/itest/java']
		resources.srcDirs = ["$rootDir/test-resources"]
		compileClasspath += sourceSets.test.output
		runtimeClasspath += sourceSets.test.output
	}
	// JMH benchmarks
	jmh {
		java.srcDirs = ['src/jmh/java']
		resources.srcDirs = ['src/jmh/resources']
		compileClasspath += sourceSets.main.runtimeClasspath
	}
}

configurations {
	itestImplementation.extendsFrom testImplementation
	itestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
	implementation project(':echosvg-i18n')
	implementation project(':echosvg-extension')
	implementation project(':echosvg-transcoder')
	api project(':echosvg-swing')
	implementation "io.sf.jclf:jclf-text:${jclfTextVersion}"
	// Required by tests
	implementation project(':echosvg-codec')
	testImplementation testFixtures(project(':echosvg-util'))
	testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testImplementation "org.mozilla:rhino:${rhinoVersion}"
	testImplementation "nu.validator:htmlparser:${htmlParserVersion}"
	if (!project.getPluginManager().hasPlugin('eclipse')) {
		testImplementation ("xml-apis:xml-apis:$xmlApisVersion")
	}
	itestImplementation project(':echosvg-test')
	jmhImplementation "org.openjdk.jmh:jmh-core:${jmhVersion}"
	jmhAnnotationProcessor "org.openjdk.jmh:jmh-generator-annprocess:${jmhVersion}"
	jmhImplementation project(':echosvg-anim')
	jmhImplementation project(':echosvg-svggen')
	jmhImplementation "xml-apis:xml-apis:$xmlApisVersion"
}

if (project.getPluginManager().hasPlugin('eclipse')) {
	configurations.all*.exclude group: 'xml-apis', module: 'xml-apis'
}

description = 'io.sf.carte:echosvg-test'

publishing.publications.maven(MavenPublication).pom {
	description = "EchoSVG Testing"
}

test {
	minHeapSize = '128m'

	useJUnitPlatform() {
		excludeTags 'SecMan'
		excludeTags 'memleak'
	}

	// Exclude tests via gradlew test -PexcludeTests="SamplesSpecRenderingTest.testVerticalText,..."
	if (project.hasProperty('excludeTests')) {
		filter {
			project.properties['excludeTests']?.replaceAll('\s', '')?.split(',').each {
				excludeTestsMatching "*.${it}"
			}
		}
	}

	testLogging {
		exceptionFormat 'full'
	}
}

tasks.withType(Test) {
	if (JavaVersion.current() > JavaVersion.VERSION_11) {
		systemProperty('java.security.manager', 'allow')
	}
}

tasks.withType(ProcessResources).configureEach {
	dependsOn ':echosvg-test-scripts:copyScriptJars',':echosvg-test-scripts:copyPolicyJar'
}

/*
 * Interactive tests runner
 */
tasks.register('iTest', Test) {
	description = 'Run interactive tests'
	group = JavaBasePlugin.VERIFICATION_GROUP

	useJUnitPlatform()
	testClassesDirs = sourceSets.itest.output.classesDirs
	classpath = sourceSets.itest.runtimeClasspath
	dependsOn configurations.itestCompileClasspath
}

/*
 * Memory leak tests
 */
tasks.register('memleakTest', Test) {
	description = 'Run memory leak tests'
	group = JavaBasePlugin.VERIFICATION_GROUP

	shouldRunAfter 'test'

	minHeapSize = '512m'
	maxHeapSize = '1G'

	useJUnitPlatform() {
		includeTags 'memleak'
	}

	testClassesDirs = testing.suites.test.sources.output.classesDirs
	classpath = testing.suites.test.sources.runtimeClasspath
}

/*
 * JMH benchmarks
 */
classes.finalizedBy(jmhClasses)

// XXX: find a way to remove the following block
compileJmhJava {
	dependsOn ':echosvg-bridge:classes'
	dependsOn ':echosvg-svggen:classes'
}

//create a single Jar with all benchmark dependencies
tasks.register('jmhJar', Jar) {
	description = 'Create a single benchmark Jar with dependencies'
	archiveBaseName = 'echosvg'
	archiveClassifier = 'jmh'
	manifest {
		attributes(
			'Main-Class' : 'org.openjdk.jmh.Main'
		)
	}
	dependsOn configurations.jmhCompileClasspath
	dependsOn configurations.jmhRuntimeClasspath
	doFirst {
		from sourceSets.jmh.output
		from {
			configurations.jmhCompileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
		}
		from {
			configurations.jmhRuntimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
		}
	}
	with jar
	duplicatesStrategy = 'exclude'
	exclude 'module-info.class'
	exclude 'javax/**'
	exclude 'org/w3c/css/**'
}

build.dependsOn jmhJar

// Execute JMH benchmarks
task runJmh(type: JavaExec) {
	description = 'Run JMH benchmarks'
	dependsOn jmhClasses
	classpath = sourceSets.jmh.runtimeClasspath
	mainClass = 'org.openjdk.jmh.Main'

	def regexp = project.properties.get('jmh.regexp', 'Mark');
	def format = project.properties.get('jmh.rf', 'json');
	def resultFilename = project.properties.get('jmh.rff', "jmh-result.${format}");
	def resultFile = file("${buildDir}/reports/jmh/${resultFilename}")

	doFirst {
		resultFile.parentFile.mkdirs()
	}

	args regexp
	args '-rf', format
	args '-rff', resultFile
	jvmArgs '-Dfile.encoding=UTF-8'
}
