plugins {
	id 'echosvg.java-conventions'
}

dependencies {
	api project(':echosvg-script')
	api project(':echosvg-ext')
	api project(':echosvg-extension')
	api project(':echosvg-gui-util')
	api project(':echosvg-swing')
	api project(':echosvg-slideshow')
	api project(':echosvg-svgpp')
	api project(':echosvg-ttf2svg')
	api project(':echosvg-svgrasterizer')
	api project(':svgom-api')
}

description = 'io.sf.carte:echosvg-all'

publishing.publications.maven(MavenPublication).pom {
	description = "EchoSVG aggregate artifact"
}

compileLegacyJava.enabled = false
checkLegacyJava.enabled = false

// Produce a Javadoc of all packages
tasks.register('mergedJavadoc', Javadoc) {
	description 'Produces a merged Javadoc of all packages'
	destinationDir = file("$buildDir/docs/javadoc")
	title = "$project.name $version API"
	dependsOn configurations.compileClasspath
	dependsOn configurations.runtimeClasspath
	includes += '**/*.java'
	excludes += '**/module-info.java'
	parent.subprojects.each { proj ->
		source += proj.layout.projectDir.dir('src/main/java')
		proj.tasks.withType(Javadoc).each { task ->
			excludes += task.excludes
			includes += task.includes
		}
	}
	classpath = configurations.compileClasspath
	classpath += configurations.runtimeClasspath
	failOnError false
}

//create a single Jar with all dependencies
tasks.register('uberjar', Jar) {
	archiveClassifier = "alldeps"
	dependsOn configurations.compileClasspath
	dependsOn configurations.runtimeClasspath
	doFirst {
		from sourceSets.main.output
		from {
			configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
		}
		from {
			configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
		}
	}
	with jar
	duplicatesStrategy = 'exclude'
	exclude 'javax/**'
	exclude 'org/w3c/css/**'
}
