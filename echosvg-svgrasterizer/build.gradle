plugins {
	id 'echosvg.java-conventions'
}

sourceSets {
	// Tests
	test {
		resources.srcDirs = ["$rootDir/test-resources"]
	}
}

dependencies {
	api project(':echosvg-transcoder-svg')
	testImplementation project(':echosvg-test-util')
	testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
	useJUnitPlatform()
}

tasks.withType(Test) {
	def curJavaVer = JavaVersion.current()
	if (curJavaVer > JavaVersion.VERSION_11 && curJavaVer < JavaVersion.VERSION_24) {
		systemProperty('java.security.manager', 'allow')
	}
}

tasks.processTestResources{
	dependsOn ':echosvg-test-scripts:copyScriptJars', ':echosvg-test-scripts:copyPolicyJar'
}

description = 'io.sf.carte:echosvg-svgrasterizer'

publishing.publications.maven(MavenPublication).pom {
	description = "EchoSVG SVG Rasterizer Library"
}

jar {
	manifest {
		attributes('Main-Class': 'io.sf.carte.echosvg.apps.rasterizer.Main')
	}
}

tasks.named('echosvg-svgrasterizer-jar-with-deps') {
	manifest {
		attributes('Main-Class': 'io.sf.carte.echosvg.apps.rasterizer.Main')
	}
}
